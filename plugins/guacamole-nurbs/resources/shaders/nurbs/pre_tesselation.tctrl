@include "resources/shaders/common/header.glsl"           
                                                               
///////////////////////////////////////////////////////////////////////////////                                         
// input
///////////////////////////////////////////////////////////////////////////////        
in vec3  vertex_position[];                  
in uint  vertex_index[];                    
in vec2  vertex_tesscoord[];   
      
///////////////////////////////////////////////////////////////////////////////
// output
///////////////////////////////////////////////////////////////////////////////                                                            
layout(vertices = 4) out; 

out vec3 control_position[];
out uint control_index[];
out vec2 control_tesscoord[];
out vec3 control_final_tesselation[];

///////////////////////////////////////////////////////////////////////////////
// uniforms
///////////////////////////////////////////////////////////////////////////////                                                            
uniform samplerBuffer parameter_texture;  
uniform samplerBuffer attribute_texture;              
uniform samplerBuffer obb_texture;
          
uniform float gua_tesselation_max_error;   
uniform float gua_max_pre_tesselation;
                                                          
uniform float gua_texel_width;                    
uniform float gua_texel_height;     
                                  
@include "resources/glsl/trimmed_surface/parametrization_uniforms.glsl"
@include "resources/glsl/common/obb_area.glsl"                          
@include "resources/glsl/trimmed_surface/estimate_edge_length.glsl"
@include "resources/glsl/common/conversion.glsl" 

@include "resources/shaders/nurbs/patch_attribute_ssbo.glsl"
@include "resources/shaders/common/gua_camera_uniforms.glsl"   
                                                                                                 
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
///////////////////////////////////////////////////////////////////////////////
void main() 
{        
  // passthrough patch information                                                                       
  control_position[gl_InvocationID]  = vertex_position[gl_InvocationID];                                               
  control_index[gl_InvocationID]     = vertex_index[gl_InvocationID];                                                  
  control_tesscoord[gl_InvocationID] = vertex_tesscoord[gl_InvocationID];                                              
                 
  // project oriented boudning box to screen and estimate area 
  mat4 mvp_matrix = gua_projection_matrix * gua_model_view_matrix;             
  int obb_index = retrieve_obb_index(int(vertex_index[gl_InvocationID]));
  float area = calculate_obb_area(mvp_matrix, gua_model_view_inverse_matrix, obb_texture, obb_index, false);
  float area_pixels = gua_resolution.x * gua_resolution.y * area;

  // derive desired tesselation based on projected area estimate
  float total_tess_level = sqrt(area_pixels) / gua_tesselation_max_error;

#if GPUCAST_SCALE_TEXXELLATION_FACTOR_TO_TRIM_RATIO
  int trim_index    = retrieve_trim_index(int(control_index[gl_InvocationID]));

  vec4 trim_domain  = texelFetch(trim_partition, trim_index+1);
  vec4 nurbs_domain = texelFetch(trim_partition, trim_index+2);

  float trim_size   = (trim_domain[1] - trim_domain[0]) * (trim_domain[3] - trim_domain[2]);
  float domain_size = (nurbs_domain[1] - nurbs_domain[0]) * (nurbs_domain[3] - nurbs_domain[2]);

  total_tess_level *= clamp(trim_size / domain_size, 0.0, 1.0);
#endif

  // assume final tesselation performs 64 subdivisions
  float pre_tess_level = total_tess_level / GPUCAST_HARDWARE_TESSELATION_LIMIT; 

  // clamp remaining pretesselations to max
  pre_tess_level = clamp(pre_tess_level, 1.0, gua_max_pre_tesselation);
  float final_tess_level = total_tess_level / pre_tess_level;

#if GPUCAST_SKIP_PRETESSELATION
  pre_tess_level = 1.0;
#endif

  // in low-quality shadow mode - don't bother with much tesselation
  if ( gua_rendering_mode == 1 ) {
    pre_tess_level = 1.0; 
    final_tess_level = total_tess_level / 16.0;
  }

  // in high-quality shadow mode - render @ 1/4 of the desired tesselation quality
  if ( gua_rendering_mode == 2 ) {
    pre_tess_level = 1.0;
    final_tess_level = total_tess_level / 4.0;
  }

  gl_TessLevelInner[0] = pre_tess_level;
  gl_TessLevelOuter[1] = pre_tess_level;
  gl_TessLevelOuter[3] = pre_tess_level;
  gl_TessLevelInner[1] = pre_tess_level;
  gl_TessLevelOuter[0] = pre_tess_level;
  gl_TessLevelOuter[2] = pre_tess_level;               
  
  // compute edge length to estimate tesselation
  int surface_index   = 0;
  int surface_order_u = 0;
  int surface_order_v = 0;
  retrieve_patch_data(int(vertex_index[gl_InvocationID]), surface_index, surface_order_u, surface_order_v);

  vec4 edge_lengths = estimate_edge_lengths_in_pixel(surface_index, 
                                                     parameter_texture, 
                                                     surface_order_u, 
                                                     surface_order_v,
                                                     gua_model_view_projection_matrix,
                                                     vec2(gua_resolution));
  edge_lengths /= pre_tess_level;

  control_final_tesselation[gl_InvocationID] = vec3(final_tess_level,
                                                    uintBitsToFloat(uint2ToUInt(uvec2(edge_lengths.xy))),
                                                    uintBitsToFloat(uint2ToUInt(uvec2(edge_lengths.zw))));
}